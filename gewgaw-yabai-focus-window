#!/bin/sh

yabai -m query --windows --space |awk -F: '
    function swap_windows(i, j, tmp) {
        tmp = window_ids[i];
        window_ids[i] = window_ids[j];
        window_ids[j] = tmp;
    }
    function sort_windows(i, j, delta) {
        for (i = 0; i < window_count; i++) {
            for (j = i+1; j < window_count; j++) {
                delta = windows[window_ids[j],"x"] - windows[window_ids[i],"x"];
                if (delta < 0)
                    swap_windows(i, j);
                else if (delta == 0) {
                    delta = windows[window_ids[j],"y"] - windows[window_ids[i],"y"];
                    if (delta < 0)
                        swap_windows(i, j);
                }
            }
        }
    }
    BEGIN{
        window_count = 0;
        keys["h"] = "yabai -m window --focus west";
        keys["j"] = "yabai -m window --focus south";
        keys["k"] = "yabai -m window --focus north";
        keys["l"] = "yabai -m window --focus east";

        keys["p"] = "yabai -m window --focus recent";
        keys["r"] = "yabai -m window --focus last";

        keys["c"] = "yabai -m display --focus 1";
        keys["m"] = "yabai -m display --focus 1";
        keys["b"] = "yabai -m display --focus 2";

        keys[","] = "kitty @ --to unix:/Users/jfelice/.run/kitty send-text  --match=title:kak_repl_window \"\x10\x0d\"";
        keys["="] = "yabai -m space --balance";
        keys["/"] = "yabai -m window --toggle split";

        keys["sh"] = "yabai -m window --swap west";
        keys["sj"] = "yabai -m window --swap south";
        keys["sk"] = "yabai -m window --swap north";
        keys["sl"] = "yabai -m window --swap east";

        keys["sp"] = "yabai -m window --swap recent";
        keys["sr"] = "yabai -m window --swap last";
    }
    {
        gsub(/[," \t]/, "", $0);
    }
    $1 == "id" {
        window_id = $2;
        window_ids[window_count] = window_id;
        window_count++;
    }
    $1 == "x"     { windows[window_id,"x"] = $2; }
    $1 == "y"     { windows[window_id,"y"] = $2; }
    $1 == "w"     { windows[window_id,"w"] = $2; }
    $1 == "h"     { windows[window_id,"h"] = $2; }
    $1 == "space" { windows[window_id,"space"] = $2; }
    END{
        sort_windows();
        cmdline = "gewgaw ";
        for (i in window_ids) {
            window_id = window_ids[i];
            cmdline = cmdline sprintf(" %d,%s,%s,%s,%s,%s",
                                      windows[window_id,"space"],
                                      windows[window_id,"x"],
                                      windows[window_id,"y"],
                                      windows[window_id,"w"],
                                      windows[window_id,"h"],
                                      i);
        }
        cmdline |getline choice;
        close(cmdline);
        gsub(/\n/, "", choice);
        if (choice in keys)
            system(keys[choice]);
        else if ("s" == choice) {
            cmdline |getline choice;
            gsub(/\n/, "", choice);
            if (("s" choice) in keys)
                system(keys["s" choice]);
            else if (1 == match(choice, /^[0-9]$/))
                system("yabai -m window --swap " window_ids[choice]);
        } else if (1 == match(choice, /^[0-9]$/))
            system("yabai -m window --focus " window_ids[choice]);
    }

'
